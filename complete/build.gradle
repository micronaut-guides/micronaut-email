plugins {
    id "com.diffplug.eclipse.apt" version "3.22.0"
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id "application"
}

version "0.1"
group "example.micronaut"

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

dependencies {
    annotationProcessor(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    annotationProcessor("io.micronaut:micronaut-inject-java")

    annotationProcessor("io.micronaut:micronaut-graal")
    compileOnly(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    compileOnly("org.graalvm.nativeimage:svm")
    implementation(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    implementation("io.micronaut:micronaut-inject")

    //tag::validator[]
    annotationProcessor("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-validation")
    //end::validator[]

    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-http-server-netty")
    implementation("io.micronaut:micronaut-http-client")
    runtimeOnly("ch.qos.logback:logback-classic")
    testAnnotationProcessor(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    testAnnotationProcessor("io.micronaut:micronaut-inject-java")
    testImplementation(platform("io.micronaut:micronaut-bom:$micronautVersion"))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("io.micronaut.test:micronaut-test-junit5")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")

    //tag::awsses[]
    implementation('software.amazon.awssdk:ses:2.13.39')
    //end::awsses[]
    //tag::sendgrid[]
    implementation('com.sendgrid:sendgrid-java:4.6.0')
    //end::sendgrid[]

}

test.classpath += configurations.developmentOnly

mainClassName = "example.micronaut.Application"

// use JUnit 5 platform
test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.toVersion('1.8')
    targetCompatibility = JavaVersion.toVersion('1.8')
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.addAll([
        '-parameters',
        // enables incremental compilation
        '-Amicronaut.processing.incremental=true',
        '-Amicronaut.processing.annotations=example.micronaut.*',
        "-Amicronaut.processing.group=$project.group",
        "-Amicronaut.processing.module=$project.name",
    ])
}

shadowJar {
    mergeServiceFiles()
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
    if (gradle.startParameter.continuous) {
        systemProperties(
            'micronaut.io.watch.restart':'true',
            'micronaut.io.watch.enabled':'true',
            "micronaut.io.watch.paths":"src/main"
        )
    }
    if (System.getProperty('sendgrid.apikey') != null) {
        systemProperty "sendgrid.apikey", System.getProperty('sendgrid.apikey')
    }
    if (System.getProperty('sendgrid.fromemail') != null) {
        systemProperty "sendgrid.fromemail", System.getProperty('sendgrid.fromemail')
    }
    if (System.getProperty('aws.accessKeyId') != null) {
        systemProperty "aws.accessKeyId", System.getProperty('aws.accessKeyId')
    }
    if (System.getProperty('aws.secretAccessKey') != null) {
        systemProperty "aws.secretAccessKey", System.getProperty('aws.secretAccessKey')
    }
    if (System.getProperty('aws.region') != null) {
        systemProperty "aws.region", System.getProperty('aws.region')
    }
    if (System.getProperty('aws.sourceemail') != null) {
        systemProperty "aws.sourceemail", System.getProperty('aws.sourceemail')
    }
}

test {
    systemProperty "sendgrid.apikey", System.getProperty('sendgrid.apikey')
    systemProperty "sendgrid.fromemail", System.getProperty('sendgrid.fromemail')
    systemProperty "aws.accessKeyId", System.getProperty("aws.accessKeyId")
    systemProperty "aws.secretAccessKey", System.getProperty("aws.secretAccessKey")
    systemProperty "aws.region", System.getProperty("aws.region")
    systemProperty "aws.sourceemail", System.getProperty("aws.sourceemail")
}

